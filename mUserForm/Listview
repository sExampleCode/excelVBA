Sub ListViewInitial()
    'lvFile 초기화
    Me.lvData.ListItems.Clear
    With Me.lvData
        .ColumnHeaders.Clear
        .ColumnHeaders.Add , , "연번", 30
        .ColumnHeaders.Add , , "구분", 86
        .ColumnHeaders.Add , , "장소", 86
        .ColumnHeaders.Add , , "조치일자", 86
        .ColumnHeaders.Add , , "장애내용", 86
        .ColumnHeaders.Add , , "조치내용", 86
        .ColumnHeaders.Add , , "결과", 86
        .ColumnHeaders.Add , , "비고", 86
        .ColumnHeaders.Add , , "파일경로", 70
        .ColumnHeaders.Add , , "파일이름", 70
        
        .LabelEdit = lvwManual      'Automatic 수정가능 lvwManual 수정불가
        .FullRowSelect = True       '전체 행 선택
        .View = lvwReport                   
        .Gridlines = True           '라인 표시여부
    End With
    
     'lvFile 초기화
    Me.lvFile.ListItems.Clear
    With Me.lvFile
        .ColumnHeaders.Clear
        .ColumnHeaders.Add , , "No", 30
        .ColumnHeaders.Add , , "File", 325
        
        .LabelEdit = lvwManual
        .FullRowSelect = True
        .View = lvwReport
        .Gridlines = True
    End With
    
End Sub
'lstDB 데이터 가져오기
Sub Get_lstDB()
    Dim rngDB As Range
    Dim rowCnt As Long, colCnt As Long
    Dim i As Long, j As Long
    Dim li As ListItem
    
    Set rngDB = Sheet3.Range("a1").CurrentRegion
    
    rowCnt = rngDB.Rows.Count
    colCnt = rngDB.Columns.Count
    
    Me.lstDB.ListItems.Clear
    
    For i = 2 To rowCnt
        Set li = Me.lstDB.ListItems.Add(, , rngDB(i, 1))
        For j = 2 To colCnt
            li.ListSubItems.Add , , rngDB(i, j)
        Next j
    Next i
    
    Set rngDB = Nothing
End Sub

'lstFile 클릭시
Private Sub lstFile_Click()

    If Me.lstFile.ListItems.Count > 0 Then
        Dim rngPICTURE As Range
        Dim wsPICTURE As Worksheet
        Dim shp As Shape
        Dim sFilePath As String
        
        Set rngPICTURE = Sheet4.Range("a1:q13")
            
        With rngPICTURE
            sFilePath = Me.txtPath & "\" & Me.lstFile.SelectedItem.SubItems(1)
            
            '파일이 있다면
            If sFilePath <> vbNullString Then
                '특정영역 이미지(도형) 삭제
                Call Del_Shps_In_Range(rngPICTURE)
                
                Set wsPICTURE = Sheet4
                
                '사진삽입
                Set shp = wsPICTURE.Shapes.AddPicture(sFilePath, msoFalse, msoCTrue, rngPICTURE.Left + 2, rngPICTURE.Top + 2, rngPICTURE.Width - 4, rngPICTURE.Height - 4)
            End If
        End With
        
         '입력값 초기화
        Call Init_InputValue
        
        '행번호 추가
        Call LastNo
        
        '버튼활성화 입력
        Call BtnInsert_Enable
        
    End If
    
    Set PICTURE = Nothing
    Set wsPICTURE = Nothing
    Set shp = Nothing
    
End Sub

'그림파일만 lstFile ListView 로 가져오기
Private Sub txtPath_DropButtonClick()
    Dim filePath As String
    Dim FSO As FileSystemObject
    Dim fl As Folder
    Dim f As File
    Dim index As Long
    Dim li As ListItem
    Dim sExtension As String
    
    With Application.FileDialog(msoFileDialogFolderPicker)
        .AllowMultiSelect = False   '한개의 파일만 선택 가능
        .Title = "이미지 폴더를 선택하세요."
        
        If .Show = -1 Then
            filePath = .SelectedItems(1)
        End If
    End With
    
    '선택하지 않고 취소한 경우 종료함.
    If filePath = vbNullString Then Exit Sub
    
    '텍스트박스에 폴더경로를 넣음
    Me.txtPath = filePath
    
    Set FSO = New FileSystemObject
    Set fl = FSO.GetFolder(Me.txtPath)
    
    'lstFile 모든 내용 지우기
    Me.lstFile.ListItems.Clear
    
    For Each f In fl.Files
        sExtension = FSO.GetExtensionName(f.Name)
        
        'jpg, jpeg, png 파일만 가능
        If LCase(sExtension) = "jpg" Or LCase(sExtension) = "jpeg" Or LCase(sExtension) = "png" Then
            Set li = Me.lstFile.ListItems.Add(, , index + 1)
            li.ListSubItems.Add , , f.Name
            
            index = index + 1
        End If
    Next
    
    Set FSO = Nothing
    Set fl = Nothing
End Sub
